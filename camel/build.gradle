plugins {
    id 'java'
    id 'maven'
}

ext {
    junit_ver = "5.6.2"
    camel_ver = "3.2.0"
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc
}

task fatJar(type: Jar) {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            groupId = 'com.chesapeaketechnology'
            artifactId = 'dds'
            version = rootProject.version

            artifact tasks.sourceJar
            artifact tasks.javadocJar

            // Allow user to define their own camel version
            //  - Its specified in the readme any version >= 3.0.0 works
            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.artifactId.text() == 'camel-core'
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
}

dependencies {
    // implementation as we won't want to include these in the packaged output for deployment
    implementation group: 'javax.enterprise', name: 'cdi-api', version: '2.0'
    implementation group: 'org.apache.camel', name: 'camel-core', version: "${camel_ver}"

    testImplementation group: 'org.apache.camel', name: 'camel-test-junit5', version: "${camel_ver}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junit_ver}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junit_ver}"
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.3.3'
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}