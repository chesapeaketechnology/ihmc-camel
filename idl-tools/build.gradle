plugins {
    id 'java'
    id 'application'
}

mainClassName = "com.chesapeaketechnology.idl.Tool"

// Task to generate Java sources from IDL files
//  - Assumes directory structure matches default values
task generateSources(dependsOn: classes, type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args 'generate'
}
// Task to process compiled sources, adding additional functions
//  - Assumes directory structure matches default values
task processGenerated(dependsOn: generateSources, type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args 'process', '--hashcode', '--copy', '--constructor'
}
// Task to generate Java classes from Java sources
//  - Assumes directory structure matches default values
task compileSources(dependsOn: processGenerated, type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args 'compile'
}
// Task to package compiled sources into a jar
//  - Assumes directory structure matches default values
task packageCompiled(dependsOn: compileSources, type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args 'pack'
}
// Task to compile the idl-tools into a runnable program
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            groupId = 'com.chesapeaketechnology'
            artifactId = 'idl'
            version = rootProject.version

            artifact tasks.sourceJar
            artifact tasks.javadocJar
        }
    }
}

dependencies {
    compile group: "us.ihmc", name: "ihmc-pub-sub-generator", version: "$ihmc_version"
    compile group: 'info.picocli', name: 'picocli', version: '4.2.0'
    compile group: 'com.github.javaparser', name: 'javaparser-core', version: '3.15.21'
}